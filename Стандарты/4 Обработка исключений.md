
# Обработка исключений

## Много кода в блоке `Попытка`

Не размещаем весь свой код метода внутрь *попытки*, это усложняет отслеживание багов

```bsl
// Неправильно
Попытка
    Сотрудник = ОсновнойСотрудник();
    СотрудникОбъект = Сотрудник.ПолучитьОбъект();
    СотрудникОбъект.Наименование = "Иванов";
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
КонецПопытки;

// Убери всё, кроме `СотрудникОбъект.Записать()` из попытки, так ты раньше
// узнаешь об ошибке и быстрее её исправишь

//Правильно
Сотрудник = ОсновнойСотрудник();
СотрудникОбъект = Сотрудник.ПолучитьОбъект();
СотрудникОбъект.Наименование = "Иванов";
Попытка
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
КонецПопытки;
```

## Правильное исключение

Любое исключение сопровождаем записью в журнал регистрации

```bsl
// Неправильно
Попытка
    Сотрудник = ОсновнойСотрудник();
    СотрудникОбъект = Сотрудник.ПолучитьОбъект();
    СотрудникОбъект.Наименование = "Иванов";
    СотрудникОбъект.Записать();
Исключение
    // Теперь ты не узнаешь, что пошло не так именно в этот момент времени
    Возврат;
КонецПопытки;

//Правильно
Сотрудник = ОсновнойСотрудник();
СотрудникОбъект = Сотрудник.ПолучитьОбъект();
СотрудникОбъект.Наименование = "Иванов";
Попытка
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
КонецПопытки;
```

## Подавление исключений

По возможности следует избегаем подавления исключения, лучше добавить больше проверок. Если подавление исключения всё таки требуется (например мы записываем пользователей в цикле), то обязательно делаем запись в журнал регистрации

```bsl
// Неправильно
Попытка
    Сотрудник = ОсновнойСотрудник();
    СотрудникОбъект = Сотрудник.ПолучитьОбъект();
    СотрудникОбъект.Наименование = "Иванов";
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
    Возврат;
КонецПопытки;

//Правильно
Сотрудник = ОсновнойСотрудник();
// Так ты не засоряешь журнал и делаешь код более прозрачным
Если Не ЗначениеЗаполнено(Сотрудник) Тогда
    Возврат;
КонецЕсли;
СотрудникОбъект = Сотрудник.ПолучитьОбъект();
СотрудникОбъект.Наименование = "Иванов";
Попытка
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
КонецПопытки;
```

## Транзакции в блоке `Попытка`

Общие правила описания транзакций. [Подробнее](https://its.1c.ru/db/v8std/content/783/hdoc) у 1С на эту тему.

1. Все манипуляции с транзакциями должны происходить в контексте одного метода
2. Метод *НачатьТранзакцию()* обязательно должен быть вне *Попытки*;
3. В блоке *Исключение* обязательно должно быть *ОтменитьТранзакцию()*
4. Метод *ЗафиксироватьТранзакцию()* должен быть после блока *Попытка-КонецПопытки*
5. Перед использованием методов *ЗафиксироватьТранзакцию()* или *ОтменитьТранзакцию()* обязательно должна быть проверка условия *ТранзакцияАктивна()*

```bsl
// Неправильно
Пока Выборка.Следующий() Цикл
    Попытка
        // !НачатьТранзакцию() внутри попытки
        НачатьТранзакцию();
        // ...
        // !ЗафиксироватьТранзакцию() внутри попытки
        // !Нет проверки на ТранзакцияАктивна()
        ЗафиксироватьТранзакцию();
        // ...
    Исключение
        // !Нет отмены транзакции, будет ошибка "В данной 
        // транзакции уже происходили ошибки"
        // Нет записи в журнал регистрации, вы не узнаете об ошибке
        Продолжить;
    КонецПопытки;
КонецЦикла;

//Правильно
Пока Выборка.Следующий() Цикл
    НачатьТранзакцию();
    Попытка        
        // ...
    Исключение
        Если ТранзакцияАктивна() Тогда
            ОтменитьТранзакцию();
        КонецЕсли;
        // ЗаписьОшибкиВЖурнал...
        Продолжить;
    КонецПопытки;
    Если ТранзакцияАктивна() Тогда
        ЗафиксироватьТранзакцию();
    КонецЕсли;
КонецЦикла;
```

Навигация:

| [Начало](../README.md) |
[Модули](./1%20Модули.md) |
[Переменные](./2%20Переменные.md) |
[Логические выражения](./3%20Логические%20выражения.md) |
[Обработка исключений](./4%20Обработка%20исключений.md) |
[Процедуры и функции](./5%20Процедуры%20и%20функции.md) |
[Безопасный код](./6%20Безопасный%20код.md) |
[Совместная разработка](./7%20Совместная%20разработка.md) |
[Стиль кодирования](/%D0%A1%D1%82%D0%B8%D0%BB%D1%8C%20%D0%BA%D0%BE%D0%B4%D0%B8%D1%80%D0%BE%D0%B2%D0%B0%D0%BD%D0%B8%D1%8F.md) |
