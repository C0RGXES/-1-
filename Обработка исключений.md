# Обработка исключений

## Много кода в *Попытке*

Не нужно помещать весь свой код внутрь попытки, это усложняет отслеживание багов:

```bsl
// Неправильно
Попытка
    Сотрудник = ОсновнойСотрудник();
    СотрудникОбъект = Сотрудник.ПолучитьОбъект();
    СотрудникОбъект.Наименование = "Иванов";
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
КонецПопытки;

// Убери всё, кроме записать из попытки, так ты раньше
// узнаешь об ошибке и быстрее её исправишь

//Правильно
Сотрудник = ОсновнойСотрудник();
СотрудникОбъект = Сотрудник.ПолучитьОбъект();
СотрудникОбъект.Наименование = "Иванов";
Попытка
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
КонецПопытки;
```

## Правильное исключение

Любое исключение должно сопровождаться записью в журнал регистрации:

```bsl
// Неправильно
Попытка
    Сотрудник = ОсновнойСотрудник();
    СотрудникОбъект = Сотрудник.ПолучитьОбъект();
    СотрудникОбъект.Наименование = "Иванов";
    СотрудникОбъект.Записать();
Исключение
    // Теперь ты не узнаешь, что пошло не так именно в этот момент времени
    Возврат;
КонецПопытки;

//Правильно
Сотрудник = ОсновнойСотрудник();
СотрудникОбъект = Сотрудник.ПолучитьОбъект();
СотрудникОбъект.Наименование = "Иванов";
Попытка
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
КонецПопытки;
```

## Подавление исключений

По возможности следует избегать подавления исключений, лучше добавить больше проверок:

```bsl
// Неправильно
Попытка
    Сотрудник = ОсновнойСотрудник();
    СотрудникОбъект = Сотрудник.ПолучитьОбъект();
    СотрудникОбъект.Наименование = "Иванов";
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
    Возврат;
КонецПопытки;

//Правильно
Сотрудник = ОсновнойСотрудник();
// Так ты не засоряешь журнал и делаешь код более прозрачным
Если Не ЗначениеЗаполнено(Сотрудник) Тогда
    Возврат;
КонецЕсли;
СотрудникОбъект = Сотрудник.ПолучитьОбъект();
СотрудникОбъект.Наименование = "Иванов";
Попытка
    СотрудникОбъект.Записать();
Исключение
    // ЗаписьВЖурналРегистрации...
КонецПопытки;
```

## Транзакции с *Попыткой*

1. Метод *НачатьТранзакцию()* обязательно должен быть вне *Попытки*;
2. В блоке *Исключение* обязательно должно быть *ОтменитьТранзакцию()*;
3. Метод *ЗафиксироватьТранзакцию()* должен быть после блока *Попытка-КонецПопытки*;
4. Перед использованием методов *ЗафиксироватьТранзакцию()* или *ОтменитьТранзакцию()* обязательно должна быть проверка условия *ТранзакцияАктивна()*;

```bsl
// Неправильно
Пока Выборка.Следующий() Цикл
    Попытка
        // !НачатьТранзакцию() внутри попытки
        НачатьТранзакцию();
        // ...
        // !ЗафиксироватьТранзакцию() внутри попытки
        // !Нет проверки на ТранзакцияАктивна()
        ЗафиксироватьТранзакцию();
        // ...
    Исключение
        // !Нет отмены транзакции, будет ошибка "В данной 
        // транзакции уже происходили ошибки"
        // Нет записи в журнал регистрации, вы не узнаете об ошибке
        Продолжить;
    КонецПопытки;
КонецЦикла;

//Правильно
Пока Выборка.Следующий() Цикл
    НачатьТранзакцию();
    Попытка        
        // ...
    Исключение
        Если ТранзакцияАктивна() Тогда
            ОтменитьТранзакцию();
        КонецЕсли;
        // ЗаписьОшибкиВЖурнал...
        Продолжить;
    КонецПопытки;
    Если ТранзакцияАктивна() Тогда
        ЗафиксироватьТранзакцию();
    КонецЕсли;
КонецЦикла;
```
